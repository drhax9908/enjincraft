buildscript {
    ext.versions = [
            'minecraftCommons': '1.0.0-fixed',
            'enjinSdk'        : '1.0.4',
            'kyoriText'       : '4.1.1',
            'kyoriTextAdapter': '4.0.0-SNAPSHOT',
            'spigot'          : '1.13.2-R0.1-SNAPSHOT',
            'placeholderApi'  : '2.10.9',
            'sqlite'          : '3.28.0',
            'sentry'          : '1.7.27',
            'nbtapi'          : '2.9.2'
    ]

    ext.deps = [
            'minecraftCommons'        : "com.enjin:spigot-commons:${versions.minecraftCommons}",
            'enjinSdk'                : "com.enjin:blockchain-sdk:${versions.enjinSdk}",
            'kyoriText'               : "net.kyori:adventure-api:${versions.kyoriText}",
            'kyoriTextAdapterBukkit'  : "net.kyori:adventure-platform-bukkit:${versions.kyoriTextAdapter}",
            'spigot'                  : "org.spigotmc:spigot-api:${versions.spigot}",
            'placeholderApi'          : "me.clip:placeholderapi:${versions.placeholderApi}",
            'sqlite'                  : "org.xerial:sqlite-jdbc:${versions.sqlite}",
            'sentry'                  : "io.sentry:sentry:${versions.sentry}",
            'nbtapi'                  : "de.tr7zw:item-nbt-api:${versions.nbtapi}"
    ]

    ext.local = new Properties()
    File file = project.rootProject.file('local.properties')
    if (file.exists()) local.load(file.newDataInputStream())

    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
}

plugins {
    id 'io.freefair.lombok' version '6.3.0'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

allprojects {
    group = 'com.enjin.enjincraft'
    version = '1.1.1'

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/groups/public/' }
	maven {
            url = uri("https://maven.pkg.github.com/drhax9908/Enjin-Minecraft-Commons")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
        maven { url "https://jitpack.io" }
	    maven { url "https://plugins.gradle.org/m2/" }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'io.freefair.lombok'
    apply plugin: 'com.github.johnrengelman.shadow'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    dependencies {
        implementation deps.sentry
    }

    javadoc {
        dependsOn delombok
        source = delombok.target
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        archiveBaseName = jar.archiveBaseName
        archiveClassifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        archiveBaseName = jar.archiveBaseName
        archiveClassifier = 'sources'
        from sourceSets.main.allJava
    }

    shadowJar {
        archiveBaseName = jar.archiveBaseName
    }

    artifacts {
        archives javadocJar
        archives sourcesJar
        archives shadowJar
    }


}


wrapper {
    gradleVersion = '7.3.3'
    distributionType = Wrapper.DistributionType.ALL
}
